import java.util.stream.Collectors


buildscript {

    apply from: 'gradle/versions.gradle'

    ext {
        artifactGroup = "com.jdiazcano.konfig"
        artifactVersion = "0.2"
    }

    repositories {
        mavenLocal()
        mavenCentral()
        jcenter()
        maven { url 'https://jetbrains.jfrog.io/jetbrains/spek-snapshots' }
        maven { url "http://dl.bintray.com/kotlin/kotlin-eap-1.1" }
    }

    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$versions.jetbrains.kotlin"
        classpath "org.junit.platform:junit-platform-gradle-plugin:$versions.junitrunner"
    }

}


plugins {
    id 'com.github.kt3k.coveralls' version '2.8.1'
    id 'com.github.ben-manes.versions' version '0.15.0'
}

apply plugin: 'jacoco'

repositories {
    mavenCentral()
}

// Generate Gradle wrapper
task wrapper(type: Wrapper) {
    gradleVersion = "3.3"
}

subprojects {
    apply plugin: 'java'
    apply plugin: 'kotlin'
    apply plugin: 'jacoco'
    apply plugin: 'org.junit.platform.gradle.plugin'

    sourceCompatibility = 1.8

    repositories {
        mavenLocal()
        mavenCentral()
        jcenter()
        maven { url 'https://jetbrains.jfrog.io/jetbrains/spek-snapshots' }
        maven { url "http://dl.bintray.com/kotlin/kotlin-eap-1.1" }
    }

    project.afterEvaluate {
        def junitPlatformTestTask = project.tasks.getByName('junitPlatformTest')

        jacoco {
            toolVersion = "+"
            reportsDir = file("$buildDir/reports/jacoco")
            applyTo junitPlatformTestTask
        }

        project.task(type: JacocoReport, "junitPlatformJacocoReport", {
            sourceDirectories = files("src/main/kotlin")
            classDirectories = files("$buildDir/classes/main")
            reports {
                xml.enabled = true
                html.enabled = true
                xml.destination "${buildDir}/reports/jacoco/test/jacocoTestReport.xml"
                html.destination "${buildDir}/reports/jacoco/test/jacocoTestReport.html"
            }
            executionData junitPlatformTestTask
        })
    }

    coveralls {
        sourceDirs += ['src/main/kotlin']
    }

    // Needed because there's something messy with kotlin version numbers and dependencies
    configurations.all {
        resolutionStrategy {
            eachDependency { DependencyResolveDetails details ->
                if (details.requested.group == 'org.jetbrains.kotlin') {
                    details.useVersion "$versions.jetbrains.kotlin"
                }
            }
        }
    }

    junitPlatform {
        platformVersion "${versions.junitrunner}"
        filters {
            engines {
                include 'spek'
            }
        }
    }

}

def activeSubprojects = subprojects.findAll { it.name.startsWith("cfg4k") }
def subprojectsExecFiles = files( activeSubprojects.stream().map { "${it.name}/build/jacoco/junitPlatformTest.exec" }.collect(Collectors.toList()) )

task junitTest(dependsOn: getTasksByName("junitPlatformTest", true))
task report(dependsOn: getTasksByName("junitPlatformJacocoReport", true))

task jacocoRootReport(type: JacocoReport) {
    additionalSourceDirs = files(activeSubprojects.sourceSets.main.allSource.srcDirs)
    sourceDirectories = files(activeSubprojects.sourceSets.main.allSource.srcDirs)
    classDirectories = files(activeSubprojects.sourceSets.main.output)
    executionData = subprojectsExecFiles
    reports {
        html.enabled = true
        xml.enabled = true
        csv.enabled = false
    }
    onlyIf = {
        true
    }

}

coveralls {
    sourceDirs = activeSubprojects.sourceSets.main.allSource.srcDirs.flatten()
    jacocoReportPath = "${buildDir}/reports/jacoco/jacocoRootReport/jacocoRootReport.xml"
}