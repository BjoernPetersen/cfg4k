buildscript {
    ext.kotlin_version = '1.1-M03'

    repositories {
        mavenCentral()
        jcenter()
        maven { url 'https://jetbrains.jfrog.io/jetbrains/spek-snapshots' }
        maven { url "http://dl.bintray.com/kotlin/kotlin-eap-1.1" }
    }
    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath 'org.junit.platform:junit-platform-gradle-plugin:1.0.0-M3'
    }
}

plugins {
    id 'com.github.kt3k.coveralls' version '2.7.1'
}

group 'com.jdiazcano'
version '0.1-SNAPSHOT'

apply plugin: 'java'
apply plugin: 'kotlin'
apply plugin: 'jacoco'
apply plugin: 'org.junit.platform.gradle.plugin'

sourceCompatibility = 1.8

repositories {
    mavenCentral()
    mavenLocal()
    jcenter()
    maven { url 'https://jetbrains.jfrog.io/jetbrains/spek-snapshots' }
    maven { url "http://dl.bintray.com/kotlin/kotlin-eap-1.1" }
}

dependencies {
    compile "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
    compile "org.jetbrains.kotlin:kotlin-test:$kotlin_version"
    compile 'org.jetbrains.spek:spek-api:1.1.0-beta2'
    compile 'com.beust:klaxon:0.24'

    testCompile 'org.junit.platform:junit-platform-runner:1.0.0-M3'
    testCompile 'com.winterbe:expekt:0.5.0'
    testRuntime 'org.jetbrains.spek:spek-junit-platform-engine:1.1.0-beta2'
}

configurations.all {
    resolutionStrategy {
        eachDependency { DependencyResolveDetails details ->
            if (details.requested.group == 'org.jetbrains.kotlin') {
                details.useVersion '1.1-M03'
            }
        }
    }
}

project.afterEvaluate {
    def junitPlatformTestTask = project.tasks.getByName('junitPlatformTest')

    jacoco {
        toolVersion = "+"
        reportsDir = file("$buildDir/reports/jacoco")
        applyTo junitPlatformTestTask
    }

    project.task(type: JacocoReport, "junitPlatformJacocoReport", {
        sourceDirectories = files("./src/main/kotlin")
        classDirectories = files("$buildDir/classes/main")
        reports {
            xml.enabled = true
            html.enabled = true
            xml.destination "${buildDir}/reports/jacoco/test/jacocoTestReport.xml"
            html.destination "${buildDir}/reports/jacoco/test/jacocoTestReport.html"
        }
        executionData junitPlatformTestTask
    })
}